buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.robolectric:robolectric-gradle-plugin:1.1.0'
    }
}

apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
def codeName = "Enchilada"

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'
    defaultConfig {
        applicationId "com.nightscout.android"
        minSdkVersion 18
        targetSdkVersion 18
        versionCode 22
        versionName "0.2.0-alpha10"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "VERSION_CODENAME", "\"${codeName}\""
    }
    lintOptions {
        quiet false
        checkAllWarnings true
        warningsAsErrors false
        abortOnError true
        textReport true
        textOutput 'stdout'
        htmlOutput file("lint-report.html")
        htmlReport true
        checkReleaseBuilds true
        warning 'ProtectedPermissions', 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }
}

repositories {
    maven { url "https://repo.eclipse.org/content/repositories/paho-releases/" }
    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    compile(project(':core')) {
        // JSON already included by adk.
        exclude group: 'org.json'
        // HttpClient already included by adk.
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'joda-time', module: 'joda-time'
    }
    compile 'ch.acra:acra:4.6.1'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.google.android.gms:play-services-analytics:7.0.0'
    compile 'org.mongodb:mongo-java-driver:2.10.1'
    compile 'joda-time:joda-time:2.5'
    compile 'net.tribe7.seeds:seeds-functional:16.0.1'
    compile 'net.tribe7.seeds:seeds-primitives:16.0.1'
    compile 'org.eclipse.paho:mqtt-client:0.4.0'
    compile 'com.noveogroup.android:android-logger:1.3.4'
    compile 'com.squareup.dagger:dagger:1.2.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.embarkmobile:zxing-android-minimal:2.0.0@aar'
    compile 'com.embarkmobile:zxing-android-legacy:2.0.0@aar'
    compile 'com.embarkmobile:zxing-android-integration:2.0.0@aar'
    compile 'com.google.zxing:core:3.1.0'
    compile 'com.getpebble:pebblekit:2.6.0@aar'
    compile 'io.reactivex:rxjava:1.0.0'
    compile 'it.neokree:MaterialNavigationDrawer:1.3.3'
    compile 'com.github.satyan:sugar:1.3'

    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'junit:junit:4.12'
    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

apply plugin: 'idea'

idea {
    module {
        testOutputDir = file('build/test-classes/debug')
    }
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/jacoco")
}

sourceSets {
    main {
        java.srcDirs = ['src']
    }
}

def coverageSourceDirs = [
        '../app/src/main/java'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "test") {
    group = "Reporting"

    description = "Generate Jacoco app coverage reports"

    classDirectories = fileTree(
            dir: '../app/build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/*$InjectAdapter*.*',
                       '**/*$ModuleAdapter*.*',
                       '**/BuildConfig.*',
                       '**/USB/*.*',
                       '**/model/**',
                       '**/Manifest*.*']
    )

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('../app/build/jacoco/testDebug.exec')

    reports {
        xml.enabled = true
        html.enabled = true
    }
}
